plugins {
  id 'org.springframework.boot' version '2.3.0.RELEASE' apply false
  id 'io.spring.dependency-management' version '1.0.9.RELEASE' apply false
  id 'idea'
}

ext {
   set('springCloudVersion', "Hoxton.SR4")
}

allprojects {
  repositories {
    mavenCentral()
  }

  version = '0.0.1-SNAPSHOT'
}


subprojects {
  // We'll need dependencies for Docker and Docker compose

  afterEvaluate { project ->
    if (project.usesSpringBoot) {
      println("Configuring ${springCloudVersion}")
      configure(project) {
        apply plugin: 'org.springframework.boot'
        apply plugin: 'io.spring.dependency-management'
        apply plugin: 'java'

        group = 'com.daecabhir'
        sourceCompatibility = '11'

        dependencies {
          implementation 'org.springframework.boot:spring-boot-starter-actuator'
          implementation 'org.springframework.boot:spring-boot-starter-web'
          implementation 'org.springframework.cloud:spring-cloud-starter-config'
          developmentOnly 'org.springframework.boot:spring-boot-devtools'
          testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
          }
        }

        dependencyManagement {
          imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
          }
        }

        test {
          useJUnitPlatform()
        }
      }
    }
  }
}

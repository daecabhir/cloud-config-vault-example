plugins {
  id 'idea'
  id 'org.springframework.boot' version '2.3.0.RELEASE' apply false
  id 'io.spring.dependency-management' version '1.0.9.RELEASE' apply false
  id 'org.unbroken-dome.test-sets' version '3.0.1' apply false
  id 'com.avast.gradle.docker-compose' version '0.13.4'
  id 'io.freefair.lombok' version '5.3.0' apply false
}

ext {
   set('springCloudVersion', "Hoxton.SR4")
}

allprojects {
  repositories {
    mavenCentral()
  }

  apply {
    plugin 'idea'
    plugin 'io.freefair.lombok'
    plugin 'com.avast.gradle.docker-compose'
  }

  // Default plugin configuration for all subprojects
  dockerCompose {
    useComposeFiles = [ 'docker-compose.yml']
    waitForTcpPorts = true
    captureContainersOutput = true
  }

  version = '0.0.1-SNAPSHOT'
}

def springBootProjects = [
  project(":cloud-config-server"),
  project(":cloud-config-client")
]

springBootProjects.each { springBootProject ->
  configure(springBootProject) {
    println("Configuring $name as a Spring Boot project")

    apply {
      plugin 'java'
      plugin 'org.springframework.boot'
      plugin 'io.spring.dependency-management'
      plugin 'org.unbroken-dome.test-sets'
    }

    group = "com.daecabhir"
    sourceCompatibility = "11"

    dependencies {
      implementation 'org.springframework.boot:spring-boot-starter-actuator'
      implementation 'org.springframework.boot:spring-boot-starter-web'

      developmentOnly 'org.springframework.boot:spring-boot-devtools'

      testImplementation 'org.hamcrest:hamcrest:2.2'
      testImplementation('org.springframework.vault:spring-vault-core') {
        exclude group: 'commons-logging', module: 'commons-logging'
      }
      testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
      }
    }

    dependencyManagement {
      imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        mavenBom 'org.springframework.vault:spring-vault-dependencies:2.2.2.RELEASE'
      }
    }

    tasks {
      withType(JavaCompile) {
        options.compilerArgs << '-Xlint:unchecked'
        options.deprecation = true
      }
      withType(Test) {
        useJUnitPlatform()
        testLogging {
          exceptionFormat = 'full'
        }
        failFast = false
      }
    }

    testSets {
      integrationTest {
        dirName = "integration-test"
      }
    }

    integrationTest {
      doLast {
        dockerCompose.composeDown(integrationTest)
      }
    }

    composeUp {
      dependsOn bootJar
    }

    bootRun {
      doLast {
        dockerCompose.composeDown(bootRun)
      }
    }

    dockerCompose {
      bootRun {
        useComposeFiles = [ 'docker-compose.dev.yml' ]
        isRequiredBy project.tasks.bootRun
      }

      integrationTest {
        useComposeFiles = [ 'docker-compose.dev.yml' ]
        isRequiredBy project.tasks.integrationTest
      }
    }
  }
}
